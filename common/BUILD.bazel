load("@rules_distroless//distroless:defs.bzl", "cacerts", "group", "home", "locale", "os_release", "passwd")
load(":variables.bzl", "matrix", "variables")

package(default_visibility = ["//visibility:public"])

[
    os_release(
        name = "os_release_%s" % dist,
        content = {
            key: value.format(
                CODENAME = codename,
                VERSION = version,
            )
            for (key, value) in variables.OS_RELEASE.items()
        },
    )
    for (dist, codename, version) in variables.VERSIONS
]

[
    locale(
        name = "locale_%s" % combined,
        # From Debian 12 the C.UTF-8 locale has been renamed into C.utf8:
        # https://metadata.ftp-master.debian.org/changelogs//main/g/glibc/glibc_2.36-9+deb12u1_changelog
        # >   * debian/debhelper.in/libc-bin.install, debian/rules.d/build.mk,
        # >     debian/rules: rename the C.UTF-8 locale into C.utf8 to match upstream
        # >     naming.
        charset = "C.UTF-8" if dist == "debian11" else "C.utf8",
        package = "@%s_%s_libc-bin//:data" % (arch, dist),
    )
    for (combined, dist, arch) in matrix.DIST_ARCH_ALL
]

[
    cacerts(
        name = "cacerts_%s" % combined,
        package = "@%s_%s_ca-certificates//:data" % (arch, dist),
    )
    for (combined, dist, arch) in matrix.DIST_ARCH_ALL
]

# create /etc/group with the root, tty, and staff groups
group(
    name = "group",
    groups = [
        {
            "name": "root",  # root_group
            "gid": variables.ROOT,
            "users": [],
        },
        {
            "name": "nobody",  # nobody_group
            "gid": variables.NOBODY,
            "users": [],
        },
        {
            "name": "nonroot",  # nonroot_group
            "gid": variables.NONROOT,
            "users": [],
        },
        {
            "name": "tty",  # tty_group
            "gid": 5,
            "users": [],
        },
        {
            "name": "staff",  # staff_group
            "gid": 50,
            "users": [],
        },
    ],
)

passwd(
    name = "passwd",
    passwds = [
        {
            "gecos": ["nobody"],
            "gid": variables.NOBODY,
            "home": "/nonexistent",
            "shell": "/sbin/nologin",
            "uid": variables.NOBODY,
            "username": "nobody",
        },
        {
            "gecos": ["root"],
            "gid": variables.ROOT,
            "shell": "/sbin/nologin",
            "home": "/root",
            "uid": variables.ROOT,
            "username": "root",
        },
        {
            "gecos": ["nonroot"],
            "gid": variables.NONROOT,
            "home": "/home/nonroot",
            "shell": "/sbin/nologin",
            "uid": variables.NONROOT,
            "username": "nonroot",
        },
    ],
)

home(
    name = "home",
    dirs = [
        {
            "home": "./root",
            "uid": variables.ROOT,
            "gid": variables.ROOT,
        },
        {
            "home": "./home/nonroot",
            "uid": variables.NONROOT,
            "gid": variables.NONROOT,
        },
    ],
)
